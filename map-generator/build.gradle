plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

mainClassName = 'mapgenerator.Main'

repositories {
    jcenter()
}

test {
    useJUnitPlatform()
    jacoco {
        excludes = ['MapGeneratorUi', 'Main']
    }
}

sourceSets {
    performanceTest {
        java.srcDir file("src/performance-test/java")
        resources.srcDir file("src/performance-test/resources")
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    performanceTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    performanceTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    dependsOn test
    
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/MapGeneratorUi**', '**/Main**'])
        }))
    }
}

javafx {
    version = "15.0.1"
    modules = [ 'javafx.controls' ]
}

jar {
    manifest {
        attributes 'Main-Class': 'mapgenerator.Main'
    }
}

task performanceTest(type: Test) {
    description = 'Ajaa suorituskykytestit'
    testClassesDirs = sourceSets.performanceTest.output.classesDirs
    classpath = sourceSets.performanceTest.runtimeClasspath
    jvmArgs '-Xss512M'
    useJUnitPlatform()
    outputs.upToDateWhen {false}
    testLogging {
        showStandardStreams = true
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.encoding = 'UTF-8'

applicationDefaultJvmArgs = ["-Xss512m"]